#version 330 core
out vec4 FragColor;

in vec2 TexCoords;

uniform sampler2D gPosition;
uniform sampler2D gNormal;
uniform sampler2D gAlbedoSpec;
uniform sampler2D gMRA;

const int N_Light_Point = 4;
const int N_Light_Dir = 2;

struct Light_Point {
    vec3 Position;
    vec3 Color;
    
    float Strength;
    float Linear;
    float Quadratic;
};
uniform Light_Point light_points[N_Light_Point];

struct Light_Dir {
    vec3 dir;
    vec3 color;

    float strength;
};
uniform Light_Dir light_dirs[N_Light_Dir];

uniform vec3 view_pos;

uniform float ambient_strength;
uniform vec3 ambient_color;

uniform int show_debug;

vec3 fresnelSchlick(float cosTheta, vec3 F0);
float DistributionGGX(vec3 N, vec3 H, float roughness);
float GeometrySchlickGGX(float NdotV, float roughness);
float GeometrySmith(vec3 N, vec3 V, vec3 L, float roughness);

void main() {             
    // retrieve data from gbuffer
    vec3 FragPos = texture(gPosition, TexCoords).rgb;
    vec3 Normal = texture(gNormal, TexCoords).rgb;
    vec3 Diffuse = texture(gAlbedoSpec, TexCoords).rgb;
    vec3 MRA = texture(gMRA, TexCoords).rgb;
    float Specular = texture(gAlbedoSpec, TexCoords).a;
    
    // If the normal is (0.0, 0.0, 0.0) that means it's an empty pixel.
    // so we just discard the pixel
    if (Normal == vec3(0, 0, 0)) { discard; }

    // then calculate lighting as usual
    vec3 lighting  = Diffuse * ambient_color * ambient_strength;
    vec3 viewDir  = normalize(view_pos - FragPos);

    for(int i = 0; i < N_Light_Point; ++i) {
        
        // diffuse
        vec3 lightDir = normalize(light_points[i].Position - FragPos);
        vec3 diffuse = max(dot(Normal, lightDir), 0.0) * Diffuse * light_points[i].Color;
        
        // specular
        vec3 halfwayDir = normalize(lightDir + viewDir);  
        float spec = pow(max(dot(Normal, halfwayDir), 0.0), 16.0);
        vec3 specular = light_points[i].Color * spec * Specular;
        
        // attenuation
        float distance = length(light_points[i].Position - FragPos);
        float attenuation =
            light_points[i].Strength / (1.0 + light_points[i].Linear * distance + light_points[i].Quadratic * distance * distance);
        
        lighting += (diffuse + specular) * attenuation;
    }
    
    for (int i = 0; i < N_Light_Dir; ++i) {
        vec3 diffuse = max(dot(Normal, light_dirs[i].dir), 0.0) * Diffuse * light_dirs[i].color;
        
        // specular
        vec3 halfwayDir = normalize(light_dirs[i].dir + viewDir);  
        float spec = pow(max(dot(Normal, halfwayDir), 0.0), 16.0);
        vec3 specular = light_dirs[i].color * spec * Specular;
        
        lighting += (diffuse + specular) * light_dirs[i].strength;
    }

    if (show_debug == 0){
        FragColor = vec4(lighting, 1.0);
    }
    if (show_debug == 1){
        FragColor = vec4(FragPos, 1.0);
    }
    if (show_debug == 2){
        FragColor = vec4(Normal, 1.0);
    }
    if (show_debug == 3){
        FragColor = vec4(Diffuse, 1.0);
    }
    if (show_debug == 4){
        FragColor = vec4(MRA, 1.0);
    }
}

vec3 fresnelSchlick(float cosTheta, vec3 F0) {
    return F0 + (1.0 - F0) * pow(1.0 - cosTheta, 5.0);
} 
float DistributionGGX(vec3 N, vec3 H, float roughness) {
    float a      = roughness*roughness;
    float a2     = a*a;
    float NdotH  = max(dot(N, H), 0.0);
    float NdotH2 = NdotH*NdotH;
    
    float num   = a2;
    float denom = (NdotH2 * (a2 - 1.0) + 1.0);
    denom = PI * denom * denom;
    
    return num / denom;
}
float GeometrySchlickGGX(float NdotV, float roughness) {
    float r = (roughness + 1.0);
    float k = (r*r) / 8.0;

    float num   = NdotV;
    float denom = NdotV * (1.0 - k) + k;
    
    return num / denom;
}
float GeometrySmith(vec3 N, vec3 V, vec3 L, float roughness) {
    float NdotV = max(dot(N, V), 0.0);
    float NdotL = max(dot(N, L), 0.0);
    float ggx2  = GeometrySchlickGGX(NdotV, roughness);
    float ggx1  = GeometrySchlickGGX(NdotL, roughness);
    
    return ggx1 * ggx2;
}
